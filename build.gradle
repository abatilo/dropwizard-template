buildscript {
  repositories {
    // Which websites to look for the Gradle dependencies to download
    jcenter()
  }
  dependencies {
    // For building the entire project, use the shadow Gradle plugin
    // Shadow is used to take all of your dependencies and turn them into
    // a single .jar file which you can run and execute
    classpath "com.github.jengelman.gradle.plugins:shadow:5.1.0"
  }
}

// All projects (including the root project as specified in
// ./settings.gradle will have these settings
allprojects {
  repositories {
    mavenCentral()
  }

  // Assume every project is a java project since Gradle supports more than
  // just Java
  apply plugin: "java"

  // Use Java 8 as the source version to build with, and the goal/target version
  sourceCompatibility = "1.8"
  targetCompatibility = "1.8"
}

// All projects, but excludes the root project
subprojects {
  group = "io.aaronbatilo.dropwizard-template"
}

// Configuration for the project that's in the folder ./dropwizard-template
project(":dropwizard-template") {
  // Make this project shadow compatible
  apply plugin: "com.github.johnrengelman.shadow"

  // For shadow to work, you need to use the application plugin
  // The application plugin lets you define mainClassName which shadow will
  // read, to understand where your project starts.
  apply plugin: "application"

  // Name of the file to start with. If you have a package name, use the fully
  // qualified package name here.
  mainClassName = "DropwizardApplication"

  run {
    args = ['server', 'local.yaml']
  }

  // Configurations for what the shadow plugin will create
  shadowJar {
    // The name of the file to create
    archiveName = "root.jar"
    // Where to put it, in this case, inside the current project directory
    destinationDir projectDir

    mergeServiceFiles()
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    manifest {
      attributes("Main-Class": mainClassName)
    }
  }

  dependencies {
    // Guava has a bunch of nice to have functions that make things easier
    compile "com.google.guava:guava:29.0-jre"

    // Lombok has some annotations that can save some time
    compileOnly("org.projectlombok:lombok:1.18.14")
    annotationProcessor("org.projectlombok:lombok:1.18.14")

    // Dropwizard is the web server. It handles receiving and responding to
    // requests and such
    compile "io.dropwizard:dropwizard-core:2.0.13"
    compile "io.dropwizard.metrics:metrics-core:4.1.13"
    compile "io.dropwizard:dropwizard-json-logging:2.0.13"

    compile "io.prometheus:simpleclient_dropwizard:0.9.0"
    compile "io.prometheus:simpleclient_servlet:0.9.0"

    // JUnit is a unit testing framework
    testCompile "junit:junit:5.4.2"
  }
}
